#-
An implementation of the Benchmark Game's spectral norm program
https://benchmarksgame-team.pages.debian.net/benchmarksgame/description/spectralnorm.html#spectralnorm

Adapted from the Lua implementation by Mike Pall
https://benchmarksgame-team.pages.debian.net/benchmarksgame/program/spectralnorm-lua-1.html
-#

A = |i, j|
  ij = i + j - 1
  1.0 / (ij * (ij - 1) * 0.5 + i)

Av = |x, y, n|
  for i in 0..n
    i1 = i + 1
    a = 0
    for j in 0..n
      a = a + x[j] * A(i1, j + 1)
    y[i] = a

Atv = |x, y, n|
  for i in 0..n
    i1 = i + 1
    a = 0
    for j in 0..n
      a = a + x[j] * A(j + 1, i1)
    y[i] = a

AtAv = |x, y, t, n|
  Av x, t, n
  Atv t, y, n

N = number env.args[0]
u = ref [1 for _ in 0..N] # TODO ref list.repeat 1, N
v = ref [0 for _ in 0..N]
t = ref [0 for _ in 0..N]

for _ in 0..10
  AtAv u, v, t, N
  AtAv v, u, t, N

vBv, vv = 0, 0
for ui, vi in u, v
  vBv = vBv + ui * vi
  vv = vv + vi * vi

result = math.sqrt vBv / vv
# print result
