program = _{
  SOI
  ~ block*
  ~ empty_line*
  ~ EOI
}

block = @{
  empty_line*
  ~ assignment_or_expression ~ (next_assignment_or_expression)*
}
child_block = @{
  empty_line*
  ~ push_indentation
  ~ (map_value | block)
  ~ DROP
}

expression_block = @{
  empty_line*
  ~ push_indentation
  ~ expression
  ~ (WHITESPACE* ~ ",")?
  ~ next_expression*
  ~ DROP
}
next_expression = @{
  NEWLINE
  ~ empty_line*
  ~ indentation
  ~ expression
  ~ (WHITESPACE* ~ ",")?
}

push_indentation = @{ indentation ~ PUSH(indent) }
indentation = @{ PEEK_ALL }
indent = _{ WHITESPACE+ }
empty_line = @{ NEWLINE | (WHITESPACE+ ~ NEWLINE) | (WHITESPACE* ~ comment) }

assignment_or_expression = _{ (assignment | expressions) ~ comment? }
next_assignment_or_expression = @{
  NEWLINE
  ~ empty_line*
  ~ indentation
  ~ assignment_or_expression
}

assignment = _{ single_assignment | multiple_assignment }
single_assignment = !{
  global_keyword?
  ~ id ~ "="
  ~ (expressions | expression_block)
}
multiple_assignment = !{
  global_keyword?
  ~ ids ~ "="
  ~ (expressions | expression_block) }

expression = _{
  ( function
  | if_statement
  | for_loop
  | operation
  | call
  )
  ~ comment?
}
expressions = !{ expression ~ ("," ~ expression?)* }

operation = ${
   (term ~ operation_link+)
   | (term ~ operation_link?)
    ~ (WHITESPACE* ~ comment)?
}
operations = !{ operation ~ ("," ~ operation)* }
operation_link = _{
  (NEWLINE* ~ WHITESPACE+ | empty_line* ~ WHITESPACE+)*
  ~ op ~ WHITESPACE* ~ term
}

op = _{
  add | subtract
  | multiply | divide | modulo
  | equal | not_equal
  | greater_or_equal | less_or_equal | greater | less
  | and | or
}

add = { "+" }
subtract = { "-" }
multiply = { "*" }
divide = { "/" }
modulo = { "%" }
equal = { "==" }
not_equal = { "!=" }
greater = { ">" }
greater_or_equal = { ">=" }
less = { "<" }
less_or_equal = { "<=" }
and = { "and" }
or = { "or" }

term = _{
  map
  | function_inline
  | flow_term
  | value_term
}

flow_term = _{
  if_inline
  | for_inline
}

value_term = _{(
  index
  | range
  | call
  | map_inline
  | list
  | string
  | number
  | boolean
  | id
  | ("(" ~ operation ~ ")")
  )
  ~ comment?
}
value_terms = !{
  value_term ~ ("," ~ value_term)*
}

if_statement = _{ if_block | if_inline }
if_block = !{
  if_keyword ~ expression ~ child_block
  ~ else_if_block?
  ~ else_block?
}
else_if_block = { NEWLINE* ~ else_if_keyword ~ expression ~ child_block }
else_block = { NEWLINE* ~ "else" ~ child_block }
if_inline = !{
  if_keyword ~ expression
  ~ then_keyword ~ expressions
  ~ (else_keyword ~ expressions)?
}

for_loop = _{ for_block | for_inline }
for_block = !{
  for_keyword ~ ids
  ~ in_keyword ~ value_terms
  ~ (if_keyword ~ operation)?
  ~ child_block
}
for_inline = !{
  value_terms
  ~ for_keyword ~ ids
  ~ in_keyword ~ value_terms
  ~ (if_keyword ~ operation)?
}

call = _{ call_with_parens | call_no_parens }
call_with_parens = ${((lookup | id) ~ call_args)}
call_args = !{
  ("(" ~ ")")
  | "(" ~ operation ~ ("," ~ operation)* ~ ")"
}
call_no_parens = ${(id ~ WHITESPACE+ ~ operations) }

function = _{ function_block | function_inline }
function_block = !{ function_args ~ child_block }
function_inline = !{ function_args ~ expressions }
function_args = {
  "|" ~ "|"
  | "|" ~ id ~ ("," ~ id)* ~ "|"
}

map_inline = !{ "{" ~ (map_entry_inline ~ ("," ~ map_entry_inline)*)?  ~"}" }
map_entry_inline = !{ id ~ ":" ~ operation }

map = @{
  empty_line*
  ~ push_indentation
  ~ map_entries
  ~ DROP
}
map_value = !{ map_entries }
map_entries = @{
  empty_line*
  ~ map_entry
  ~ (next_map_entry)*
}
map_entry = _{ map_entry_block | map_entry_inline }
map_entry_block = !{ id ~ ":" ~ map }
next_map_entry = @{ NEWLINE ~ empty_line* ~ indentation ~ map_entry }

lookup = ${ id ~ "." ~ id }

list = !{
  "[" ~ "]"
  | "[" ~ operation ~ ("," ~ operation)* ~ "]"
}

range = ${ range_arg ~ range_op ~ range_arg }
range_op = { ("..=" | "..") }
range_arg = _{ number | call | index | lookup | id | ("(" ~ operation ~ ")") }

index = ${ id ~ "[" ~ operation ~ "]" }

boolean = { "true" | "false" }

number = @{
  "-"?
  ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
  ~ ("." ~ ASCII_DIGIT+)?
  ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
  !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}

id = ${ single_id ~ ("." ~ single_id)* }
single_id = @{ !(keyword ~ !id_end) ~ id_start ~ id_end* }
id_start = @{ ASCII_ALPHA }
id_end = @{ ASCII_ALPHANUMERIC | "_" }
ids = { id ~ ("," ~ id)* }

keyword = _{
  "and"
  | "else"
  | "false"
  | "for"
  | "global"
  | "if"
  | "in"
  | "or"
  | "then"
  | "true"
}

else_if_keyword = @{ "else if" ~ WHITESPACE+ }
else_keyword = @{ "else" ~ WHITESPACE+ }
for_keyword = @{ "for" ~ WHITESPACE+ }
global_keyword = @{ "global" ~ WHITESPACE+ }
if_keyword = @{ "if" ~ WHITESPACE+ }
in_keyword = @{ "in" ~ WHITESPACE+ }
then_keyword = @{ "then" ~ WHITESPACE+ }


WHITESPACE = _{ " " | "\t" }
comment = _{ multiline_comment | inline_comment }
inline_comment = _{ "#" ~ (!NEWLINE ~ ANY)* }
multiline_comment = _{ "#-" ~ (!"-#" ~ multiline_comment* ~ ANY)* ~ "-#" }
