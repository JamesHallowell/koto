program = _{
  SOI
  ~ block*
  ~ empty_line*
  ~ EOI
}

block = @{
  empty_line*
  ~ expression
  ~ (next_expression)*
}
child_block = @{
  empty_line*
  ~ push_indentation
  ~ block
  ~ DROP
}

push_indentation = @{ indentation ~ PUSH(indent) }
indentation = @{ PEEK_ALL }
indent = _{ WHITESPACE+ }
empty_line = @{ NEWLINE | (WHITESPACE+ ~ NEWLINE) | (WHITESPACE* ~ comment) }

next_expression = @{ NEWLINE ~ indentation ~ expression }

expression = _{
  ( assignment
  | function
  | if_statement
  | for_loop
  | operation
  | call
  )
  ~ comment?
}

operation = !{
   (term ~ (op ~ term)+)
   | (term ~ (op ~ term)?)
    ~ comment?
}

op = _{
  add | subtract | multiply | divide
  | equal | not_equal
  | greater_or_equal | less_or_equal | greater | less
  | and | or
}

add = { "+" }
subtract = { "-" }
multiply = { "*" }
divide = { "/" }
equal = { "==" }
not_equal = { "!=" }
greater = { ">" }
greater_or_equal = { ">=" }
less = { "<" }
less_or_equal = { "<=" }
and = { "and" }
or = { "or" }

term = _{(
  function_inline
  | if_inline
  | for_inline
  | index
  | array
  | range
  | string
  | number
  | boolean
  | call
  | id
  | ("(" ~ operation ~ ")")
  )
  ~ comment?
}

if_statement = _{ if_block | if_inline }
if_block = !{
  if_keyword ~ expression
  ~ child_block
  ~ (NEWLINE* ~ "else" ~ child_block)?
}
if_inline = !{
  if_keyword ~ expression
  ~ then_keyword ~ expression
  ~ (else_keyword ~ expression)?
}

if_keyword = @{ "if" ~ WHITESPACE+ }
then_keyword = @{ "then" ~ WHITESPACE+ }
else_keyword = @{ "else" ~ WHITESPACE+ }

for_loop = _{ for_block | for_inline }
for_block = !{
  for_keyword ~ id
  ~ in_keyword ~ term
  ~ (if_keyword ~ operation)?
  ~ child_block
}
for_inline = !{
  (call_with_parens | id)
  ~ for_keyword ~ id
  ~ in_keyword ~ term
  ~ (if_keyword ~ operation)?
}

for_keyword = @{ "for" ~ WHITESPACE+ }
in_keyword = @{ "in" ~ WHITESPACE+ }

call = _{ call_with_parens | call_single_arg }
call_with_parens = ${(id ~ call_args)}
call_args = !{
  ("(" ~ ")")
  | "(" ~ operation ~ ("," ~ operation)* ~ ")"
}
call_single_arg = ${(id ~ WHITESPACE+ ~ operation) }

assignment = !{ id ~ "=" ~ expression }

function = _{ function_block | function_inline }
function_block = !{ function_args ~ child_block }
function_inline = !{ function_args ~ expression }
function_args = {
  "|" ~ "|"
  | "|" ~ id ~ ("," ~ id)* ~ "|"
}

array = !{
  "[" ~ "]"
  | "[" ~ operation ~ ("," ~ operation)* ~ "]"
}

range = ${ range_arg ~ range_op ~ range_arg }
range_op = { ("..=" | "..") }
range_arg = _{ number | call | index | id | ("(" ~ operation ~ ")") }

index = ${ id ~ "[" ~ operation ~ "]" }

boolean = { "true" | "false" }

number = @{
  "-"?
  ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
  ~ ("." ~ ASCII_DIGIT+)?
  ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
  !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}

id = @{ !(keyword ~ !id_end) ~ id_start ~ id_end* }
id_start = @{ ASCII_ALPHA }
id_end = @{ ASCII_ALPHANUMERIC | "_" }

keyword = _{
  "and"
  | "else"
  | "false"
  | "for"
  | "if"
  | "in"
  | "or"
  | "then"
  | "true"
}

WHITESPACE = _{ " " | "\t" }
comment = _{ multiline_comment | inline_comment }
inline_comment = _{ "#" ~ (!NEWLINE ~ ANY)* }
multiline_comment = _{ "#-" ~ (!"-#" ~ multiline_comment* ~ ANY)* ~ "-#" }
