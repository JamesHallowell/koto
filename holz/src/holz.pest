WHITESPACE = _{ " " | "\t" }
COMMENT = _{ comment_multi | comment_single }
comment_multi = _{ "###" ~ (!"###" ~ ANY)+ ~ "###" }
comment_single = _{ "#" ~ (!NEWLINE ~ ANY)* }

keyword = _{ "and" | "or" | "class" }

program = _{ SOI ~ block* ~ NEWLINE* ~ EOI }

block = @{
  (NEWLINE | COMMENT)*
  ~ expression
  ~ (next_expression)*
}
child_block = _{ push_indentation ~ block ~ DROP }

push_indentation = ${ indentation ~ PUSH(indent) }
indentation = ${ NEWLINE ~ PEEK_ALL }
indent = _{ WHITESPACE+ }

expression = !{
  ( assignment
  | binary_op
  | function
  | term
  )
  ~ COMMENT?
}
next_expression = _{ indentation ~ expression }

term = _{
  "(" ~ expression ~ ")"
  | call
  | array
  | string
  | number
  | boolean
  | id
}

call = { (id ~ call_args) | (id ~ expression) }
call_args = _{
  "(" ~ ")"
  | "(" ~ expression ~ ("," ~ expression)* ~ ")"
}

assignment = { id ~ "=" ~ expression }

binary_op = { term ~ (arithemetic_op | logic_op) ~ expression }
arithemetic_op = {
  "+" | "-" | "/" | "*"
}

logic_op = {
  "==" | "!=" | "<=" | "<" | ">=" | ">" | "and" | "or"
}

function = { function_args ~ (expression | child_block) }
function_args = {
  "|" ~ "|"
  | "|" ~ id ~ ("," ~ id)* ~ "|"
}

array = {
  "[" ~ "]"
  | "[" ~ expression ~ ("," ~ expression)* ~ "]"
}

boolean = { "true" | "false" }

number = @{
  "-"?
  ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
  ~ ("." ~ ASCII_DIGIT*)?
  ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
  !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}

id = @{ !(keyword ~ !id_end) ~ id_start ~ id_end* }
id_start = @{ ASCII_ALPHA }
id_end = @{ ASCII_ALPHANUMERIC | "_" }
