WHITESPACE = _{ " " | "\t" }

comment = _{ comment_multi | comment_single }
comment_multi = _{ "###" ~ (!"###" ~ ANY)+ ~ "###" }
comment_single = _{ "#" ~ (!NEWLINE ~ ANY)* }

keyword = _{
  "and"
  | "class"
  | "else"
  | "for"
  | "if"
  | "in"
  | "or"
  | "then"
}

program = _{ SOI ~ block* ~ (NEWLINE | empty_line)* ~ EOI }

block = @{
  (NEWLINE | empty_line)*
  ~ expression
  ~ (next_expression)*
}
child_block = @{ (NEWLINE | (empty_line))* ~ push_indentation ~ block ~ DROP }
next_expression = @{ NEWLINE ~ indentation ~ expression }

push_indentation = ${ indentation ~ PUSH(indent) }
indentation = ${ PEEK_ALL }
indent = _{ WHITESPACE+ }
empty_line = @{ (WHITESPACE+ ~ NEWLINE) | (WHITESPACE* ~ comment) }

expression = !{
  ( assignment
  | function
  | if_block
  | for_block
  | value
  )
  ~ comment?
}


value = _{
  rhs_value | lhs_value
  ~ comment?
}

rhs_value = _{
  binary_op
}

lhs_value = _{
  (
  "(" ~ value ~ ")"
  | if_inline
  | for_inline
  | index
  | call
  | array
  | range
  | string
  | number
  | boolean
  | id
  )
  ~ comment?
}

if_block = {
  if_keyword ~ value
  ~ child_block
  ~ (NEWLINE* ~ "else" ~ child_block)?
}

if_inline = {
  if_keyword ~ value
  ~ then_keyword ~ value
  ~ (else_keyword ~ value)?
}

if_keyword = @{ "if" ~ WHITESPACE+ }
then_keyword = @{ "then" ~ WHITESPACE+ }
else_keyword = @{ "else" ~ WHITESPACE+ }

for_block = {
  for_keyword ~ id
  ~ in_keyword ~ value
  ~ (if_keyword ~ value)?
  ~ child_block
}

for_inline = {
  (call | id)
  ~ for_keyword ~ id
  ~ in_keyword ~ value
  ~ (if_keyword ~ value)?
}

for_keyword = @{ "for" ~ WHITESPACE+ }
in_keyword = @{ "in" ~ WHITESPACE+ }

call = { (id ~ call_args) | (id ~ expression) }
call_args = _{
  "(" ~ ")"
  | "(" ~ expression ~ ("," ~ expression)* ~ ")"
}

assignment = { id ~ "=" ~ expression }

binary_op = { lhs_value ~ (arithemetic_op | logic_op) ~ expression }
arithemetic_op = {
  "+" | "-" | "/" | "*"
}

logic_op = {
  "==" | "!=" | "<=" | "<" | ">=" | ">" | "and" | "or"
}

function = { function_args ~ (expression | child_block) }
function_args = {
  "|" ~ "|"
  | "|" ~ id ~ ("," ~ id)* ~ "|"
}

array = {
  "[" ~ "]"
  | "[" ~ expression ~ ("," ~ expression)* ~ "]"
}

range = ${
  range_arg ~ range_op ~ range_arg
}
range_op = {
  ("..=" | "..")
}
range_arg = _{
  number | call | index | id
}

index = ${
  id ~ "[" ~ expression ~ "]"
}

boolean = { "true" | "false" }

number = @{
  "-"?
  ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
  ~ ("." ~ ASCII_DIGIT+)?
  ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
  !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}

id = @{ !(keyword ~ !id_end) ~ id_start ~ id_end* }
id_start = @{ ASCII_ALPHA }
id_end = @{ ASCII_ALPHANUMERIC | "_" }
