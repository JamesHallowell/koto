program = _{
  SOI
  ~ block*
  ~ NEWLINE*
  ~ EOI
}

block = @{
  (NEWLINE | COMMENT)*
  ~ statement
  ~ (next_statement)*
}

statement = _{ expression }
next_statement = _{ indentation ~ statement }

indentation = ${ NEWLINE ~ PEEK_ALL }
push_indentation = ${ indentation ~ PUSH(indent) }

indent = _{ WHITESPACE+ }

child_block = _{
  push_indentation
  ~ block
  ~ DROP
}

expression = !{
  ( assignment
  | function
  | binary_op
  | term
  )
  ~ COMMENT?
}

term = _{
  "(" ~ expression ~ ")"
  | call
  | string
  | number
  | boolean
  | ident
}

boolean = { "true" | "false" }

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

call = {
  (ident ~ parameters)
  | (ident ~ expression)
}

parameters = _{
  "(" ~ ")"
  | "(" ~ expression ~ ("," ~ expression)* ~ ")"
}

assignment = { ident ~ "=" ~ expression }

binary_op = { term ~ (arithemetic_op | logic_op) ~ expression }
arithemetic_op = {
  "+" | "-" | "/" | "*"
}

logic_op = {
  "==" | "!=" | "<=" | "<" | ">=" | ">" | "and" | "or"
}

number = @{
  "-"?
  ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
  ~ ("." ~ ASCII_DIGIT*)?
  ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
  !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}

function = {
  ((function_args ~ function_op) | function_op)
  ~ (expression | child_block)
}
function_args = _{
  ident ~ ("," ~ ident)*
}
function_op = { "->" }

WHITESPACE = _{ " " | "\t" }
COMMENT = _{ ("#" ~ (!NEWLINE ~ ANY)*) }
